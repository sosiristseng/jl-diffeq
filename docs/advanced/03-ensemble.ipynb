{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Parallel Ensemble Simulations\n",
    "\n",
    "Docs: https://diffeq.sciml.ai/stable/features/ensemble/\n",
    "\n",
    "## Solving an ODE With Different Initial Conditions\n",
    "\n",
    "Solving $\\dot{u} = 1.01u$ with $u(0)=0.5$ and $t \\in [0, 1]$ with multiple initial conditions."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "using OrdinaryDiffEq\n",
    "using Plots"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Linear ODE which starts at 0.5 and solves from t=0.0 to t=1.0"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "prob = ODEProblem((u, p, t) -> 1.01u, 0.5, (0.0, 1.0))"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Define a new problem for each trajectory using `remake()`\n",
    "The initial conditions (u0) are changed in this example"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function prob_func(prob, i, repeat)\n",
    "    remake(prob, u0=rand() * prob.u0)\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "The function could also capture the necessary data outside.\n",
    "\n",
    "```julia\n",
    "initial_conditions = range(0, stop=1, length=100)\n",
    "\n",
    "function prob_func(prob, i, repeat)\n",
    "  remake(prob, u0=initial_conditions[i])\n",
    "end\n",
    "```"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Define an ensemble problem"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "ensemble_prob = EnsembleProblem(prob; prob_func=prob_func)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Ensemble simulations are solved by multithreading by default"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "sim = solve(ensemble_prob, trajectories=100)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Each element in the results is an ODE solution"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "sim[1]"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "You can plot all the results at once"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "plot(sim, linealpha=0.4)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Solving an SDE with Different Parameters"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "using StochasticDiffEq\n",
    "using Plots\n",
    "\n",
    "function lotka_volterra!(du, u, p, t)\n",
    "    du[1] = p[1] * u[1] - p[2] * u[1] * u[2]\n",
    "    du[2] = -3 * u[2] + u[1] * u[2]\n",
    "end\n",
    "\n",
    "function g!(du, u, p, t)\n",
    "    du[1] = p[3] * u[1]\n",
    "    du[2] = p[4] * u[2]\n",
    "end\n",
    "\n",
    "p = [1.5, 1.0, 0.1, 0.1]\n",
    "prob = SDEProblem(lotka_volterra!, g!, [1.0, 1.0], (0.0, 10.0), p)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function prob_func(prob, i, repeat)\n",
    "    x = 0.3 * rand(2)\n",
    "    remake(prob, p=[p[1:2]; x])\n",
    "end\n",
    "\n",
    "ensemble_prob = EnsembleProblem(prob, prob_func=prob_func)\n",
    "sim = solve(ensemble_prob, SRIW1(), trajectories=10)\n",
    "\n",
    "plot(sim, linealpha=0.5, color=:blue, idxs=(0, 1))\n",
    "plot!(sim, linealpha=0.5, color=:red, idxs=(0, 2))"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Show the distribution of the ensemble solutions"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "summ = EnsembleSummary(sim, 0:0.1:10)\n",
    "plot(summ, fillalpha=0.5)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Ensemble simulations of Modelingtoolkit (MTK) models\n",
    "Radioactive decay example"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "using ModelingToolkit\n",
    "using OrdinaryDiffEq\n",
    "using Plots\n",
    "\n",
    "@independent_variables t\n",
    "@variables c(t) = 1.0\n",
    "@parameters λ = 1.0\n",
    "D = Differential(t)\n",
    "@mtkbuild sys = ODESystem([D(c) ~ -λ * c], t)\n",
    "prob = ODEProblem(sys, [], (0.0, 2.0), []);\n",
    "solve(prob)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Use the symbolic interface to change the parameter(s)."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function changemtkparam(prob, i, repeat)\n",
    "    # Make a new copy of the parameter vector\n",
    "    # Ensure the changed will not affect the original ODE problem\n",
    "    remake(prob, p=[λ => i * 0.5])\n",
    "end\n",
    "\n",
    "eprob = EnsembleProblem(prob, prob_func=changemtkparam)\n",
    "sim = solve(eprob, trajectories=10)\n",
    "plot(sim, linealpha=0.5)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "---\n",
    "\n",
    "*This notebook was generated using [Literate.jl](https://github.com/fredrikekre/Literate.jl).*"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.12.3",
   "language": "julia",
   "name": "julia-1.12"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.12.3"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 3
}
